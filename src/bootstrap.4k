create ; 
postpone lit 1 c, postpone state postpone !
postpone last 
postpone @ 
postpone lit 
1 c, 
postpone + 
postpone last 
postpone !

postpone lit
-1 c,
postpone here
postpone @
postpone c!

postpone lit
-1 c,
postpone here
postpone @
postpone lit
1 c,
postpone +
postpone c!

immediate

last @ 1 + last ! 

create : 
       postpone create 
       postpone lit 
       0 c, 
       postpone state 
       postpone ! 
;

: [ 1 state ! ; immediate
: ] 0 state ! ;

: here@ here @ ;
: begin1 create ; immediate
: begin here@ ; immediate

: literal 0 c, c, ;
: rliteral 1 c, , ;
: sliteral swap rliteral literal ; 

: 2dup swap dup rot dup rot swap ;

: 1+ 1 + ;
: 1- 1 - ;
: 0= 0 = ;

: repeat postpone branch here@ - c, immediate ;

: a begin 1 . 10 emit repeat ;

: if postpone branch0 here@ 0 c, ; immediate
: then dup here@ swap - swap c! ; immediate 

: | begin key 10 = if ;; then repeat ; immediate

: c: key state @ 0 = if literal then ; immediate

: ( 1
    begin
	c: ) key dup rot = if swap 1- dup 0= if drop drop ;; then swap then
	c: (             = if 1+ then
    repeat ; immediate


: (* begin key c: * = if key c: ) = if ;; then then repeat ; immediate

(* 
a multi
line 
comment
*)

| comparison words
: <> = invert ;
: 0<> 0= invert ;
: <= > invert ;
: >= < invert ;
: +! swap >r dup @ r> + swap ! ;

: data: here there @ here@ there ! swap ! ;
: data; data: ;

| output words
: cr 10 emit ;

: create create there @ rliteral postpone ; ; 

| to verbose but will make optimisations later
| by default we will use byte cells, as it is meant to be 4kb tool
: cell 1 ;
: cells cell * ;
| ref cells
: rcell 4 ;
: rcells rcell * ;
: rcells+ rcells + ;

: allot there @ + there ! ;

: variable : there @ rliteral postpone ; rcell allot  ;


: r ( -- r ) postpone r> postpone dup postpone >r ; immediate

| strings

create #str
256 allot
 

: " ( -- str c ) here @ >r #str here !
  begin key dup c: " = if drop #str here @ #str - 0 c, r> here ! ;; then 
  c, repeat
; immediate


| inlines a counted list of bytes to there 
: inline ( str c -- str c ) here @ >r there @ here ! here @ + >r
  begin r here @ = if there @ r> there @ - here @ there ! r> here ! ;; then 
  dup c@ c, 1+ repeat 
;

: t 0 >r r> drop ;

: type ( str c -- ) >r begin 
  r 0 = if r> drop drop ;; then 
  r> 1 - >r dup c@ emit 1+ repeat 
; 


| pimped ", it now compiles the string to there @compile time
: " postpone " state @ 0 = if inline there @ dup 0 swap c! 1+ there ! 
   sliteral then 
; immediate 

: ." postpone " state @ 0 = if postpone type ;; then type ; immediate 


: ccall: : postpone ccall c, postpone ; ;

0 ccall: dlopen
1 ccall: dlsym
2 ccall: printf

: >cstr drop ;

: lib >cstr 1 swap dlopen ; 

| do loops

: do
    postpone swap
    postpone >r
    postpone >r
    postpone begin
    postpone r>
    postpone r>
    postpone 2dup
    postpone >r
    postpone >r
    postpone =
    postpone invert
    postpone if
; immediate


: loop
    swap
    postpone r>
    postpone 1+
    postpone >r
    postpone repeat
    postpone then
    postpone r>
    postpone r>
    postpone drop
    postpone drop
; immediate


: type' 1- 0 do dup r + c@ emit loop ;

: a " ala ma kota" type cr begin repeat ;
