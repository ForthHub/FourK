\ FourK - Concatenative, stack based, Forth like language optimised for 
\        non-interactive 4KB size demoscene presentations.

\ Copyright (C) 2009, 2010 Wojciech Meyer, Josef P. Bernhart

\ This program is free software: you can redistribute it and/or modify
\ it under the terms of the GNU General Public License as published by
\ the Free Software Foundation, either version 3 of the License, or
\ (at your option) any later version.

\ This program is distributed in the hope that it will be useful,
\ but WITHOUT ANY WARRANTY; without even the implied warranty of
\ MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
\ GNU General Public License for more details.

\ You should have received a copy of the GNU General Public License
\ along with this program.  If not, see <http://www.gnu.org/licenses/>.
forget
mark
(*
	Sections, lenght 32 bytes 
	----------------------
	| string  < 1-23 byte > + 1 Byte |
	| offset  < 4 byte >             | 
	| pointer < 4 byte>              | 
        ----------------------------------
*)

| Sections support
7 #section !

| size of section data structure
32 const section
: table ( -- ptr ) stab @ ;
: th ( section# -- ofs ) section * ;
: pointer ( section* -- sec-pointer ) section + cell - ;
: offset ( section* -- sec-offset ) pointer cell - ; 
: 1+! ( ptr -- ) 1 swap +! ;
: in ( ofs ptr -- ptr ) + ;
: >imrel ( ptr -- ofs ) imbase @ - ;
: imrel> ( ofs -- ofs ) imbase @ + ;
: section>  ( section# -- sec-ptr ) th table in ;
: current-section #section @ section> ;
: new ( str-name str-c -- )
| include the trailing zero    
    1+ 
| get the address of next section
    current-section
| set the pointer relative to image start
    dup pointer there @ >imrel swap !
    dup offset 0 swap !
| increment section count
    #section 1+!
| copy the string name into section name field
    copy
| ensure that we set zero at the next section
    0 current-section !
;

: tab 9 emit ;
: forall ( -- max min ) #section @ 1- 0 ; 
: dump ( -- ) forall do i section> dup dup dup count type pointer tab . offset @ tab . cr loop ;


: section ( -- ) token new ;

: find-section ( str c -- index )
   forall do 2dup i th table in dup 
   count str= if 2drop i leave ;; then loop 2drop -1 
;

: section: ( -- r: -- last-here section-ptr )
    token 2dup find-section dup -1 =
    if drop ." section: : Section " type ."  not defined" cr
    else
	literal 2drop
	postpone here postpone @ postpone >r
	postpone dup postpone >r
	postpone section> postpone pointer postpone @ postpone imrel>
	postpone here postpone !
    then
; immediate

: ;section
    postpone r> postpone pointer 
    postpone here postpone @
    postpone swap postpone !
    postpone r> postpone dup postpone here postpone !
;

