| FourK - Concatenative, stack based, Forth like language optimised for
|        non-interactive 4KB size demoscene presentations.
|
| Copyright (C) 2009, 2010, 2011 Wojciech Meyer, Josef P. Bernhart
|
| This program is free software: you can redistribute it and/or modify
| it under the terms of the GNU General Public License as published by
| the Free Software Foundation, either version 3 of the License, or
| (at your option) any later version.
|
| This program is distributed in the hope that it will be useful,
| but WITHOUT ANY WARRANTY; without even the implied warranty of
| MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
| GNU General Public License for more details.
|
| You should have received a copy of the GNU General Public License
| along with this program.  If not, see <http://www.gnu.org/licenses/>.
| ______________________________________________________________________
| Opening openGL context through SDL
| ______________________________________________________________________

symbol: exit
symbol: printf
symbol: dlerror
symbol: SDL_SetVideoMode
symbol: SDL_PollEvent
symbol: SDL_GL_SwapBuffers
symbol: SDL_Init
symbol: SDL_InitSubSystem
symbol: glClear

| SDL constants
27  const Esc
2   const Keydown
create event 20 bytes allot

| SDL structure dispatch
: e>type c@ ;
: e>key 8 + c@ ;
: s>pixels 20 + @ ;

: libc " libc.so" lib ;
: libGL " libGL.so" lib ;
: libSDL " libSDL.so" lib ;

: function >r sym r> cells add-handle ;

: init-display-syms ( -- )
    libc " _exit" 1 function
    libc " printf" 1 function
    libSDL " dlerror" 0 function
    libSDL " SDL_SetVideoMode" 4 function
    libSDL " SDL_PollEvent" 1 function
    libSDL " SDL_GL_SwapBuffers"     0 function
    libSDL " SDL_Init" 0 function
    libSDL " SDL_InitSubSystem" 1 function
    libGL " glClear" 1 function
;

$00000100 const GL_DEPTH_BUFFER_BIT			
$00004000 const GL_COLOR_BUFFER_BIT

: init-display
    init-display-syms
    $00000002 32 600 800 SDL_SetVideoMode drop
;
: esc-check
    event SDL_PollEvent drop
    event e>type Keydown = if event e>key Esc = ;; then
    0
;

: Render: :
    postpone init-display postpone begin
    postpone esc-check postpone if postpone ;; postpone then
;

: ;Render postpone again postpone ; ; immediate


: clear-display
    GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT or glClear drop
;

: swap-display SDL_GL_SwapBuffers drop ;
