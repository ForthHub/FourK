| FourK - Concatenative, stack based, Forth like language optimised for
|        non-interactive 4KB size demoscene presentations.
|
| Copyright (C) 2009, 2010, 2011 Wojciech Meyer, Josef P. Bernhart
|
| This program is free software: you can redistribute it and/or modify
| it under the terms of the GNU General Public License as published by
| the Free Software Foundation, either version 3 of the License, or
| (at your option) any later version.
|
| This program is distributed in the hope that it will be useful,
| but WITHOUT ANY WARRANTY; without even the implied warranty of
| MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
| GNU General Public License for more details.
|
| You should have received a copy of the GNU General Public License
| along with this program.  If not, see <http://www.gnu.org/licenses/>.
| ______________________________________________________________________
| Opening openGL context through SDL
| ______________________________________________________________________

require" lib/symbols.4k"

| SDL constants
27  const Esc
2   const Keydown
create event 20 bytes allot

variable display-width
variable display-height

| SDL structure dispatch
: e>type c@ ;
: e>key 8 + c@ ;
: s>pixels 20 + @ ;

Lib: /usr/lib/libGL.so
Lib: libSDL.so
Lib: /usr/lib/libGLU.so
4 Symbol: SDL_SetVideoMode
1 Symbol: SDL_PollEvent
1 Symbol: SDL_Init
1 Symbol: SDL_GL_GetProcAddress
1 Symbol: SDL_GL_LoadLibrary
0 Symbol: SDL_GL_SwapBuffers
8 Symbol: gluPerspective
1 Symbol: glClear
3 Symbol: glViewport
1 Symbol: glMatrixMode
0 Symbol: glLoadIdentity
12 Symbol: glFrustum
LoadSymbols

$00000100 constant GL_DEPTH_BUFFER_BIT			
$00004000 constant GL_COLOR_BUFFER_BIT
$00001700 constant GL_MODELVIEW
$00001701 constant GL_PROJECTION
$00000002 constant SDL_OPENGL

: display-ratio ( -- ratio )
    finit display-height @ >f display-width @ >f f/ f>
;

: init-display
    SDL_OPENGL 0 display-height @ display-width @ SDL_SetVideoMode drop
    
    display-height @ display-width @ 0 0 glViewport drop
    GL_PROJECTION glMatrixMode drop glLoadIdentity drop
       100.0 f>d 0.1 f>d 1.33 f>d 45.0 f>d gluPerspective drop
    GL_MODELVIEW glMatrixMode drop
    glLoadIdentity drop
;

: esc-check
    event SDL_PollEvent drop
    event e>type Keydown = if event e>key Esc = ;; then
    0
;

: Render: immediate
    postpone begin
    postpone esc-check postpone if postpone ;; postpone then
;

: Display:
    ( height width -- )
    display-width !
    display-height !
    :
    postpone init-display
;

: ;Render postpone again ; immediate
: ;Display postpone ; ; immediate


: clear-display
    GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT or glClear drop
;

: swap-display SDL_GL_SwapBuffers drop ;
