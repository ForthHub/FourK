forget
mark
: fconst : rliteral postpone >f postpone ; ;
: i>>f i>f >f ;

| Let's bind some C library symbols


| OSS Stuff
$c0045005 const SNDCTL_DSP_SETFMT
$c0045006 const SNDCTL_DSP_CHANNELS
$c0045005 const SNDCTL_DSP_SETFMT
$c0045002 const SNDCTL_DSP_SPEED
$5001     const SNDCTL_DSP_SYNC
$1        const O_WRONLY
$00000010 const AFMT_S16_LE


4096 const sample/second

1 sample/second * const #samples

2 const sample

: samples sample * ;

variable buffer
variable audio
variable temp

: w! 2dup c! swap 8 >> swap 1+ c! ;

: #sample! samples buffer @ + w! ;


: i>>f i>f >f ;


2 ccall: exit
3 ccall: SDL_SetVideoMode
4 ccall: SDL_PollEvent
5 ccall: SDL_Flip
6 ccall: SDL_LockSurface
7 ccall: SDL_UnlockSurface
8 ccall: malloc |  should be in bss section
9 ccall: printf
10 ccall: open
11 ccall: ioctl
12 ccall: write

: render-noise
    	0 SNDCTL_DSP_SYNC audio @ ioctl drop
	sample/second samples buffer @ audio @ write drop
;

: >/dev/dsp ( ctl val -- ) temp ! temp swap audio @ ioctl drop ;
: init-synth
    " libc.so" lib " malloc" sym rcell add-handle
    " libc.so" lib " printf" sym 2 rcells add-handle
    " libc.so" lib " open" sym 3 rcells add-handle
    " libc.so" lib " ioctl" sym 3 rcells add-handle
    " libc.so" lib " write" sym 3 rcells add-handle

    #samples samples malloc buffer !
    #samples 0
    do
	i 8 << i #sample!
    loop
    0 O_WRONLY " /dev/dsp" >cstr open audio !
    
    SNDCTL_DSP_SETFMT   AFMT_S16_LE >/dev/dsp
    SNDCTL_DSP_CHANNELS 1           >/dev/dsp
    SNDCTL_DSP_SPEED    11024       >/dev/dsp
;
: init ( -- )
    " libc.so" lib " _exit" sym rcell add-handle
    
    " /usr/lib/libSDL.so" lib " SDL_SetVideoMode" sym 4 rcells add-handle

    " /usr/lib/libSDL.so" lib " SDL_PollEvent" sym rcell add-handle

    " /usr/lib/libSDL.so" lib " SDL_Flip" sym rcell add-handle

    " /usr/lib/libSDL.so" lib " SDL_LockSurface" sym rcell add-handle

    " /usr/lib/libSDL.so" lib " SDL_UnlockSurface" sym rcell add-handle

    init-synth
;


| this for making happy my ans-loving Emacs mode ;) ( but I really like the | for a comment )
: \ postpone | ; immediate

\ some constants
800 const width
600 const height
32  const bpp
bpp 8 / const point

\ SDL constants
27  const Esc
2   const Keydown

\ SDL structure dispatch
: e>type c@ ;
: e>key 8 + c@ ;
: s>pixels 20 + @ ;

\ out screen surface
variable screen
\ allot needed stuff for SDL
create event 20 cells allot
\ and for pallete
create pallete 256 rcells allot

\ get the linear offset
: points point * ;
\ put the pixel in right place with pallete colour
: pixel ( col x y -- ) width * + points screen @ s>pixels + swap rcells pallete + @ swap ! ;

\ set the video mode
: video 0 bpp height width SDL_SetVideoMode ;

\ alias for convenience
: y postpone j ; immediate
: x postpone i ; immediate

\ sqr function
: sqr dup * ;

\ some more constants
width  2 / i>f fconst w/2
height 2 / i>f fconst h/2

\ our sin (or cos I dunno) function
: fsin fsincos f> drop ;

\ some pimped sin function which keeps value 0. - 255.
: sin fsin 127.0 >f f* 128.0 >f f+ ;

: dupf f> dup >f >f ;
\ factoring could be done here
: nx i>f >f w/2 f- ;
: ny i>f >f h/2 f- ;

\ some float constants
: swapf f> f> swap >f >f ;
1.0 >f 8.0 >f swapf f/ f> fconst c1/8
1.0 >f 16.0 >f swapf f/ f> fconst c1/16
1.0 >f 32.0 >f swapf f/ f> fconst c1/32

\ unused currently
variable time
1.0 time !

: isqrf i>>f dupf f* ;
\ we needed to split the word into second one because of -127 limit for a loop
: draw2
    x isqrf 
    y isqrf 
    f+ fsqrt
    c1/8 f* sin
;

| draw it!
: draw
    height 0 do
	width 0 do
	    finit
	    x i>>f c1/16 f* sin 
	    y i>>f c1/32 f* sin f+

	    x nx dupf f* 
	    y ny dupf f* 
	    f+ fsqrt
	    
	    c1/8 f* sin
	    f+
	    draw2 f+
	    
	    f> f>i 4 / x y pixel
	loop
    loop
	    
|	    0.01 >f time @ >f f+ f> time !
;

3.1415 fconst pi

: col> ( i f:fac -- f:c ) i>>f pi f* f* sin ;
: col! ( c i -- ) rcells pallete + + f> f>i swap c! ;
: color ( i f ) dup >r col> r> col! ;
\ init our pallete
: init-pallete
    256 0
    do
	finit
	0 i c1/8 color
	1 i c1/16 color
	2 i c1/32 color 
    loop
;

: draw
    screen @ SDL_LockSurface drop
    draw
    screen @ SDL_UnlockSurface drop
    screen @ SDL_Flip drop
;

: render-loop
    begin
	render-noise
	event SDL_PollEvent drop
	event e>type Keydown = if event e>key Esc = if ;; then then
repeat
;

: main
    init
    init-pallete
    video screen !
    draw
    render-loop
|    0 exit
;



